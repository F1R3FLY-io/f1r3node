// Match a list of known valid publicKey -> VaultAddress pairs to bind them to veriables
match (
    "04f700a417754b775d95421973bdbdadb2d23c8a5af46f1829b1431f5c136e549e8a0d61aa0c793f1a614f8e437711c7758473c6ceb0859ac7e9e07911ca66b5c4".hexToBytes(),
    "11112VYAt8rUGNRRZX3eJdgagaAhtWTK8Js7F7X5iqddMVqyDTtYau",
    "1111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111111".hexToBytes(),
    "1111pdZDG4MZ3eBfaipaD22VXmbFY6PW9ZdGDWdEcXZz4gGxTxgn9",
    "2222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222222".hexToBytes(),
    "11112CXAkc8pV2AMHUSgHGbFkqovgyY9SRja4ZLSpRC9q7qYZx5SVc"
) {
  (
    genesisPubKey,
    genesisVaultAddress, // the rev address of a vault instantiated at genesis
    alicePubKey,
    aliceVaultAddress, // the rev address of a vault constructed post-genesis
    bobPubKey,
    bobVaultAddress // the rev address of a vault constructed post-genesis
  ) => {

    new
      rl(`rho:registry:lookup`),
      stdlog(`rho:io:stdlog`),
      RhoSpecCh,
      ListOpsCh,
      getDeployerId(`rho:test:deployerId:make`),
      setup,
      testFindOrCreateGenesisVault,
      testDepositToGenesisCreatedVault,
      testBalanceInGenesisCreatedVault,
      testTransferInGenesisCreatedVault,
      testCreateVaultFail,
      testCreateVault,
      testTransfer,
      testFindOrCreate,
      testTransferToSelf,
      testAmountNegative,
      testInsufficeintFunds,
      testInvalidVaultAddress,
      testUnfBoundVault,
      testLogging,
      withVaultAndIdentityOf,
      transfer,
      assertBalances
    in {
      rl!(`rho:id:zphjgsfy13h1k85isc8rtwtgt3t9zzt5pjd5ihykfmyapfc4wt3x5h`, *RhoSpecCh) |
      for(@(_, RhoSpec) <- RhoSpecCh) {
        @RhoSpec!("testSuite", *setup,
          [
            ("Create genesis vault with expected balance", *testFindOrCreateGenesisVault),
            ("Return an error for invalid public key", *testCreateVaultFail),
            ("Create vault and see that it is secured", *testCreateVault),
            ("Transfer from genesis to Alice", *testTransfer),
            ("Return the existing wallet when calling testFindOrCreate", *testFindOrCreate),
            ("Transfer to own account successfully", *testTransferToSelf),
            ("Fail the tranfer if the amount to transfer is negative", *testAmountNegative),
            ("Fail the transfer if there are insufficient funds", *testInsufficeintFunds),
            ("Fail the transfer if the destination addresss is not a valid VaultAddress", *testInvalidVaultAddress),
            ("Transfer from unforgeable name bound vault", *testUnfBoundVault),
            ("Logging works", *testLogging)
          ])
      } |

      contract setup(_, ret) = {
        new SystemVaultCh in {
          rl!(`rho:system:systemVault`, *SystemVaultCh) |
          for (@(_, SystemVault) <- SystemVaultCh) {
            ret!(SystemVault)
          }
        }
      } |

      contract testFindOrCreateGenesisVault(rhoSpec, SystemVault, ackCh) = {
        new genesisVaultCh, balanceCh in {
          SystemVault!("findOrCreate", genesisVaultAddress, *genesisVaultCh) |
          for (@(true, genesisVault) <- genesisVaultCh) {
            // so far, access to genesisVault is not secured. This will be changd.
            @genesisVault!("balance", *balanceCh) |
            rhoSpec!("assert", (9000000, "== <-", *balanceCh), "balance is as expected", *ackCh)
          }
        }
      } |

      contract testCreateVaultFail(rhoSpec, SystemVault, ackCh) = {
        new e1 in {
          SystemVault!("findOrCreate", "EEEE", *e1) |
          rhoSpec!("assertMany",
            [
              (((false, "Invalid address length"), "== <-", *e1), "should be a failure")
            ], *ackCh
          )
        }
      } |

      contract testCreateVault(rhoSpec, SystemVault, ackCh) = {
        new aliceVaultCh, deployerId(`rho:system:deployerId`), genesisAuthKeyCh, balanceCh, transferCh in {
          SystemVault!("findOrCreate", aliceVaultAddress, *aliceVaultCh) |
          //as per `setup`, the deployer is `genesis` and shouldn't be able to access the vault
          SystemVault!("deployerAuthKey", *deployerId, *genesisAuthKeyCh) |
          for (@(true, aliceVault) <- aliceVaultCh & genesisAuthKey <- genesisAuthKeyCh) {
            @aliceVault!("balance", *balanceCh) |
            @aliceVault!("transfer", genesisVaultAddress, 1, *genesisAuthKey, *transferCh) |
            rhoSpec!("assertMany",
              [
                ((0, "== <-", *balanceCh), "balance should be 0"),
                (((false, "Invalid AuthKey"), "== <-", *transferCh), "transfer should fail")
              ], *ackCh
            )
          }
        }
      } |

      contract testTransfer(rhoSpec, SystemVault, ackCh) = {
        new genesisVaultCh, aliceVaultCh, ret, transferSuccessful  in {
          withVaultAndIdentityOf!(genesisPubKey, *genesisVaultCh) |
          SystemVault!("findOrCreate", aliceVaultAddress, *aliceVaultCh) |
          for (genesisVault, @genesisVaultKey <- genesisVaultCh & @(true, aliceVault) <- aliceVaultCh) {
            genesisVault!("transfer", aliceVaultAddress, 1000, genesisVaultKey, *ret) |
            rhoSpec!("assert", ((true, Nil), "== <-", *ret), "transfer successful", *transferSuccessful) |
            assertBalances!(*transferSuccessful, [(*genesisVault, 8999000), (aliceVault, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testFindOrCreate(rhoSpec, SystemVault, ackCh) = {
        new genesisVaultCh, aliceVaultCh, now in {
          //the below attempts to create a wallet are going to fetch the ones created in previous tests.
          SystemVault!("findOrCreate", genesisVaultAddress, *genesisVaultCh) |
          SystemVault!("findOrCreate", aliceVaultAddress, *aliceVaultCh) |
          for (@(true, g) <- genesisVaultCh & @(true, a) <- aliceVaultCh) {
            now!(Nil) |
            assertBalances!(*now, [(g, 8999000), (a, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testTransferToSelf(rhoSpec, SystemVault, ackCh) = {
        new aliceVaultCh, now, balanceOk, transferResult, transferOk in {
          withVaultAndIdentityOf!(alicePubKey, *aliceVaultCh) |
          for (@aliceVault, @aliceVaultKey <- aliceVaultCh) {
            now!(Nil) |
            assertBalances!(*now, [(aliceVault, 1000)], *rhoSpec, *balanceOk) |
            transfer!(*balanceOk, aliceVault, aliceVaultAddress, 1000, aliceVaultKey, *transferResult) |
            rhoSpec!("assert", ((true, Nil), "== <-", *transferResult), "transfer successful", *transferOk) |
            assertBalances!(*transferOk, [(aliceVault, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testAmountNegative(rhoSpec, SystemVault, ackCh) = {
        new aliceVaultCh, now, balanceOk, transferResult, transferOk in {
          withVaultAndIdentityOf!(alicePubKey, *aliceVaultCh) |
          for (@aliceVault, @aliceVaultKey <- aliceVaultCh) {
            now!(Nil) |
            assertBalances!(*now, [(aliceVault, 1000)], *rhoSpec, *balanceOk) |
            transfer!(*balanceOk, aliceVault, bobVaultAddress, -1, aliceVaultKey, *transferResult) |
            rhoSpec!("assert", ((false, "Amount must be positive value"), "== <-", *transferResult), "transfer failed", *transferOk) |
            assertBalances!(*transferOk, [(aliceVault, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testInsufficeintFunds(rhoSpec, SystemVault, ackCh) = {
        new aliceVaultCh, res, resOk in {
          withVaultAndIdentityOf!(alicePubKey, *aliceVaultCh) |
          for (aliceVault, @aliceVaultKey <- aliceVaultCh) {
            aliceVault!("transfer", bobVaultAddress, 9000, aliceVaultKey, *res) |
            rhoSpec!("assert", ((false, "Insufficient funds"), "== <-", *res), "transfer failed", *resOk) |
            assertBalances!(*resOk, [(*aliceVault, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testInvalidVaultAddress(rhoSpec, SystemVault, ackCh) = {
        new aliceVaultCh, res, resOk in {
          withVaultAndIdentityOf!(alicePubKey, *aliceVaultCh) |
          for (aliceVault, @aliceVaultKey <- aliceVaultCh) {
            aliceVault!("transfer", "pretty surely invalid rev address", 1, aliceVaultKey, *res) |
            rhoSpec!("assert", ((false, "Invalid Base58 encoding"), "== <-", *res), "transfer failed", *resOk) |
            assertBalances!(*resOk, [(*aliceVault, 1000)], *rhoSpec, *ackCh)
          }
        }
      } |

      contract testUnfBoundVault(rhoSpec, SystemVault, ackCh) = {
        new VaultAddress(`rho:vault:address`), deployerId(`rho:system:deployerId`),
            unf, unfRevAddrCh, unfAuthKeyCh, unfVaultCh,
            genesisVaultCh, initTransferCh, transferCh
        in {
          withVaultAndIdentityOf!(genesisPubKey, *genesisVaultCh) |
          // Get unforgeable channel REV address
          VaultAddress!("fromUnforgeable", *unf, *unfRevAddrCh) |
          for (@unfVaultAddress <- unfRevAddrCh; genesisVault, genesisVaultKey <- genesisVaultCh) {
            // Transfer initial funds to unforgeable vault
            genesisVault!("transfer", unfVaultAddress, 100, *genesisVaultKey, *initTransferCh) |
            // Teting transfer from unforgeable bound vault
            SystemVault!("findOrCreate", unfVaultAddress, *unfVaultCh) |
            SystemVault!("unforgeableAuthKey", *unf, *unfAuthKeyCh) |
            for (@(true, unfVault) <- unfVaultCh; unfAuthKey <- unfAuthKeyCh; _ <- initTransferCh) {
              @unfVault!("transfer", genesisVaultAddress, 1, *unfAuthKey, *transferCh) |
              rhoSpec!("assert", ((true, Nil), "== <-", *transferCh), "transfer succeeded", *ackCh)
            }
          }
        }
      } |

      contract testLogging(rhoSpec, SystemVault, ackCh) = {
        new genesisVaultCh, aliceVaultCh, ret, transferSuccessful  in {
          withVaultAndIdentityOf!(genesisPubKey, *genesisVaultCh) |
          SystemVault!("findOrCreate", aliceVaultAddress, *aliceVaultCh) |
          for (genesisVault, @genesisVaultKey <- genesisVaultCh & @(true, aliceVault) <- aliceVaultCh) {
            new ack, logCh, assertCh1, assertCh2 in {
              genesisVault!("setLog", *logCh, genesisVaultKey, *ack) |
              for (_ <- ack) {
                genesisVault!("transfer", aliceVaultAddress, 1000, genesisVaultKey, *ret) |
                for (@["transfer", vaultAddress, amount, result, blockNumber, timestamp, sender] <- logCh) {
                  rhoSpec!("assert",
                    ([aliceVaultAddress, 1000, (true, Nil)] == [vaultAddress, amount, result]),
                    "transfer successfully logged",
                    *ackCh)
                }
              }
            }
          }
        }
      } |

      new SystemVaultCh in {
        rl!(`rho:system:systemVault`, *SystemVaultCh) |
        for (@(_, SystemVault) <- SystemVaultCh) {

          contract withVaultAndIdentityOf(@pubKey, ret) = {
            new VaultAddress(`rho:vault:address`), vaultAddrCh, vaultCh, deployerIdCh, authKeyCh in {
              getDeployerId!("deployerId", pubKey, *deployerIdCh) |
              VaultAddress!("fromPublicKey", pubKey, *vaultAddrCh) |
              for (@vaultAddr <- vaultAddrCh) {
                @SystemVault!("findOrCreate", vaultAddr, *vaultCh) |
                for (@(true, vault) <- vaultCh & deployerId <- deployerIdCh) {
                  @SystemVault!("deployerAuthKey", *deployerId, *authKeyCh) |
                  for (@authKey <- authKeyCh) {
                    ret!(vault, authKey)
                  }
                }
              }
            }
          }

        }
      } |

      contract transfer(when, vault, @address, @amount, authKey, ret) = {
        for (_ <- when) {
          vault!("transfer", address, amount, *authKey, *ret)
        }
      } |

      rl!(`rho:lang:listOps`, *ListOpsCh) |
      for(@(_, ListOps) <- ListOpsCh) {

        contract assertBalances(when, @balances, rhoSpec, ret) = {
          for (_ <- when) {
            new assertBalance in {
              @ListOps!("foreach", balances, *assertBalance, *ret) |

              contract assertBalance(@(vault, expectedBalance), retCh) = {
                new balanceCh in {
                  @vault!("balance", *balanceCh) |
                  rhoSpec!("assert", (expectedBalance, "== <-", *balanceCh), "balance is as expected", *retCh)
                }
              }
            }
          }
        }

      }
    }
  }
}

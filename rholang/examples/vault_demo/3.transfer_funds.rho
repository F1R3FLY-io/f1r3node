new
  rl(`rho:registry:lookup`), SystemVaultCh,
  stdout(`rho:io:stdout`)
in {

  rl!(`rho:vault:system`, *SystemVaultCh) |
  for (@(_, SystemVault) <- SystemVaultCh) {

    stdout!(("3.transfer_funds.rho")) |

    // REPLACE THE VAULT ADDRESSES HERE vvv
    match (
      "%FROM",
      "%TO",
      100
    ) {
      (from, to, amount) => {

        new vaultCh, targetVaultCh, authKeyCh, deployerId(`rho:system:deployerId`) in {
          @SystemVault!("findOrCreate", from, *vaultCh) |
          // make sure the target vault it created and the transfer would be done
          @SystemVault!("findOrCreate", to, *targetVaultCh) |
          @SystemVault!("deployerAuthKey", *deployerId, *authKeyCh) |
          for (@(true, vault) <- vaultCh & key <- authKeyCh & @(true, _) <- targetVaultCh) {

            stdout!(("Beginning transfer of ", amount, "tokens from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {

                stdout!(("Finished transfer of ", amount, "tokens to", to, "result was:", result))
              }
            }
          }
        }
      }
    }
  }

}

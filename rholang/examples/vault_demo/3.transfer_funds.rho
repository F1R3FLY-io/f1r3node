new
  rl(`rho:registry:lookup`), SystemVaultCh,
  stdout(`rho:io:stdout`)
in {

  rl!(`rho:system:systemVault`, *SystemVaultCh) |
  for (@(_, SystemVault) <- SystemVaultCh) {

    stdout!(("3.transfer_funds.rho")) |

    // REPLACE THE REV ADDRESSES HERE vvv
    match (
      "%FROM",
      "%TO",
      100
    ) {
      (from, to, amount) => {

        new vaultCh, targetVaultCh, systemVaultkeyCh, deployerId(`rho:system:deployerId`) in {
          @SystemVault!("findOrCreate", from, *vaultCh) |
          // make sure the target vault it created and the transfer would be done
          @SystemVault!("findOrCreate", to, *targetVaultCh) |
          @SystemVault!("deployerAuthKey", *deployerId, *systemVaultkeyCh) |
          for (@(true, vault) <- vaultCh & key <- systemVaultkeyCh & @(true, _) <- targetVaultCh) {

            stdout!(("Beginning transfer of ", amount, "REV from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {

                stdout!(("Finished transfer of ", amount, "REV to", to, "result was:", result))
              }
            }
          }
        }
      }
    }
  }

}

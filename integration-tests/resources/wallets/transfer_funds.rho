new
  rl(`rho:registry:lookup`), SystemVaultCh,
  stdout(`rho:io:stdout`), log
in {

  rl!(`rho:system:systemVault`, *SystemVaultCh) |
  for (@(_, SystemVault) <- SystemVaultCh) {

    log!(("3.transfer_funds.rho")) |

    // REPLACE THE REV ADDRESSES HERE vvv
    match (
      "%FROM",
      "%TO",
      %AMOUNT
    ) {
      (from, to, amount) => {

        new vaultCh, systemVaultkeyCh, deployerId(`rho:system:deployerId`) in {
          @SystemVault!("findOrCreate", from, *vaultCh) |
          @SystemVault!("deployerAuthKey", *deployerId, *systemVaultkeyCh) |
          for (@(true, vault) <- vaultCh; key <- systemVaultkeyCh) {

            log!(("Beginning transfer of ", amount, "REV from", from, "to", to)) |

            new resultCh in {
              @vault!("transfer", to, amount, *key, *resultCh) |
              for (@result <- resultCh) {
                match result{
                  (true, _) =>{log!("%LOG_MARKER Successfully reason: ${reason}" %%{"reason": "Nil"})}
                  (false, reason) => {log!("%LOG_MARKER Failing reason: ${reason}" %%{"reason": reason})}
                }
              }
            }
          }
        }
      }
    }
  } |

  contract log(@data) = {
    @"DEMO"!(data) | stdout!(data)
  }
}
